pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

plugins {
    id 'io.freefair.settings.plugin-versions' version '8.6'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
    id "com.gradle.enterprise" version "3.16.2"
}

rootProject.name = 'dart-sass-java'

boolean isCiServer = System.getenv().containsKey("CI")

gradleEnterprise {
    if (isCiServer) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "larsgrefer"
            password = providers.gradleProperty('larsgreferBuildCachePassword').getOrElse(null)
        }
    }
}

include "sass-embedded-protocol"
include "sass-embedded-host"
include "sass-embedded-bundled"
include "sass-embedded-bundled-ia32"

include "sass-embedded-spring5"
include "sass-embedded-spring6"

include "sass-embedded-android"

include "test-projects:spring-boot-resources"
include "test-projects:graalvm"
include "test-projects:android-app"
