plugins {
    id "com.google.protobuf" version "0.9.4"
    id "io.freefair.maven-publish-java"
    id "idea"
    id "java-library"
}

description = "SASS Embedded Protocol"

publishing.publications.mavenJava.pom.description = "Java DTOs for the embedded-sass-protocol"

repositories {
    ivy {
        //https://github.com/sass/sass/archive/refs/tags/embedded-protocol-1.2.0.zip
        artifactPattern "https://github.com/[orgPath]/[orgPath]/archive/[artifact]-[revision].[ext]"
        metadataSources {
            artifact()
        }
    }
}

def protobufVersion = "3.25.2"
def protocolVersion = "2.4.0"

tasks.withType(com.google.protobuf.gradle.ProtobufExtract).configureEach {
    doFirst {
        project.delete destDir
    }
}

dependencies {
    protobuf "sass:embedded-protocol:$protocolVersion@zip"

    api "com.google.protobuf:protobuf-java:$protobufVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

tasks.named("jar", Jar) {
    manifest {
        attributes 'Specification-Vendor': "Google LLC",
                'Specification-Version': "$protocolVersion"
    }
}
