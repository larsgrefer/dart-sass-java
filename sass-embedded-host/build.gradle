plugins {
    id "java-library"
    id "io.freefair.maven-publish-java"
}

description = "SASS Embedded Host"

configurations {
    sassEmbedded
}

repositories {
    //https://github.com/sass/dart-sass-embedded/releases/download/$dartSassVersion/sass_embedded-$dartSassVersion-linux-ia32.tar.gz
    ivy {
        url "https://github.com/sass/dart-sass-embedded/releases/download"
        patternLayout {
            artifact "/[revision]/[artifact]-[revision](-[classifier])(.[ext])"
        }
        metadataSources {
            artifact()
        }
    }
}

def dartSassVersion = "1.0.0-beta.7"

dependencies {
    api project(":sass-embedded-protocol")
    api 'org.slf4j:slf4j-api:1.7.30'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'org.springframework:spring-core:5.+'

    sassEmbedded "sass:sass_embedded:$dartSassVersion:linux-ia32@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:linux-x64@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:macos-x64@tar.gz"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:windows-ia32@zip"
    sassEmbedded "sass:sass_embedded:$dartSassVersion:windows-x64@zip"

    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}

//"https://github.com/sass/dart-sass-embedded/releases/download/1.0.0-beta.6/sass_embedded-1.0.0-beta.6-macos-x64.tar.gz

processResources {

    into("de/larsgrefer/sass/embedded") {
        from configurations.sassEmbedded

        rename "-$dartSassVersion-", "-"
    }
}
