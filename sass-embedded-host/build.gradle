plugins {
    id "java-library"
    id "io.freefair.maven-publish-java"
    id "io.freefair.lombok"
    id "jacoco"
}

description = "SASS Embedded Host"

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

compileJava.options.release.set(8)

java {
    registerFeature('webjarsSupport') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('springSupport') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('springWebmvcSupport') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    api project(":sass-embedded-protocol")
    api project(":sass-embedded-bundled")
    api "org.slf4j:slf4j-api:1.7.36"

    compileOnly 'org.jetbrains:annotations:26.0.0'
    compileOnly 'androidx.annotation:annotation-jvm:1.8.2'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.24.1'
    webjarsSupportImplementation "org.webjars:webjars-locator-core:0.59"
    compileOnly "org.jboss:jboss-vfs:3.3.2.Final"

    springSupportApi "org.springframework:spring-core:$spring5Verison"
    springWebmvcSupportApi "org.springframework:spring-webmvc:$spring5Verison"

    testCompileOnly 'org.jetbrains:annotations:26.0.0'

    testRuntimeOnly 'org.webjars:bootstrap:5.3.3'
}

tasks.named("processResources", ProcessResources) {
    expand(project.getProperties())
    inputs.property("projectVersion", project.version)
    inputs.property("embeddedProtocolVersion", embeddedProtocolVersion)
    inputs.property("dartSassVersion", dartSassVersion)
}
