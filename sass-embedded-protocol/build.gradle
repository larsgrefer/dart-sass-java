plugins {
    id "com.google.protobuf" version "0.8.18"
    id "io.freefair.maven-publish-java"
    id "idea"
    id "java-library"
}

description = "SASS Embedded Protocol"

publishing.publications.mavenJava.pom.description = "Java DTOs for the embedded-sass-protocol"

repositories {
    ivy {
        //https://github.com/sass/embedded-protocol/archive/1.0.0-beta.7.tar.gz
        artifactPattern "https://github.com/[orgPath]/[artifact]/archive/[revision].[ext]"
        metadataSources {
            artifact()
        }
    }
}

def protobufVersion = "3.19.1"
def protocolVersion = "1.0.0-beta.17"

tasks.withType(com.google.protobuf.gradle.ProtobufExtract) {
    doFirst {
        project.delete destDir
    }
}

dependencies {
    protobuf "sass:embedded-protocol:$protocolVersion@zip"

    api "com.google.protobuf:protobuf-java:$protobufVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

jar.manifest {
    attributes 'Specification-Vendor': "Google LLC",
            'Specification-Version': "$protocolVersion"
}

afterEvaluate {
    sourcesJar.from tasks.generateProto

    javadoc {
        source tasks.generateProto
        failOnError = false
    }
}
