plugins {
    id "java-library"
    id "io.freefair.maven-publish-java"
    id "io.freefair.lombok"
    id "jacoco"
}

description = "SASS Embedded Host"

configurations {
    sassEmbedded
}

repositories {
    ivy {
        //https://github.com/sass/dart-sass/releases/download/1.63.3/dart-sass-1.63.3-linux-x64.tar.gz
        url "https://github.com"
        patternLayout {
            artifact "/[orgPath]/[artifact]/releases/download/[revision]/[artifact]-[revision](-[classifier])(.[ext])"
        }
        metadataSources {
            artifact()
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(11)

compileJava.options.release.set(8)

def dartSassVersion = "1.63.3"
def slf4jVersion = '2.0.7'
def webjarsVersion = '0.52'

dependencies {
    api project(":sass-embedded-protocol")
    api "org.slf4j:slf4j-api:$slf4jVersion"

    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.20.0'
    compileOnly "org.springframework:spring-core:5.3.28"
    compileOnly "org.webjars:webjars-locator-core:$webjarsVersion"
    compileOnly "org.jboss:jboss-vfs:3.2.17.Final"

    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-ia32@tar.gz"
    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-x64@tar.gz"
    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-arm@tar.gz"
    sassEmbedded "sass:dart-sass:$dartSassVersion:linux-arm64@tar.gz"

    sassEmbedded "sass:dart-sass:$dartSassVersion:macos-x64@tar.gz"
    sassEmbedded "sass:dart-sass:$dartSassVersion:macos-arm64@tar.gz"

    sassEmbedded "sass:dart-sass:$dartSassVersion:windows-ia32@zip"
    sassEmbedded "sass:dart-sass:$dartSassVersion:windows-x64@zip"

    testImplementation "org.springframework:spring-core:5.3.28"
    testImplementation 'ch.qos.logback:logback-classic:1.4.7'

    testCompileOnly 'org.jetbrains:annotations:24.0.1'

    testRuntimeOnly "org.webjars:webjars-locator-core:$webjarsVersion"
    testRuntimeOnly 'org.webjars:bootstrap:5.3.0'
}

tasks.named("processResources") {
    into("de/larsgrefer/sass/embedded") {
        from configurations.sassEmbedded
        rename "-$dartSassVersion-", "-"
    }
}

tasks.named("jar") {
    manifest {
        attributes 'Specification-Version': "$dartSassVersion"
    }
}
